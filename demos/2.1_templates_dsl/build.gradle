buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath('com.terrafolio:gradle-jenkins-plugin:1.+')
    }
}

apply plugin: 'jenkins'

jenkins {
    // server definitions
    servers {
        demo {
            url 'http://localhost:8080'
            secure false
        }
    }

    defaultServer servers.demo

    // template definitions
    templates {
        template_job {
            dsl {
                description "Live demo...Please God, let it work"
                displayName "Template Job"
                steps {
                    shell 'echo "Gradle Rocks!"'
                }
            }
        }
    }

    // job definitions	
    jobs {
        [ "build", "release" ].each { buildType ->
            "build_${buildType}" {
                dsl {
                    using 'template_job'
                    displayName "${buildType.capitalize()} Job"
                    steps {
                        gradle(buildType)
                    }
                }
            }
        }
    }
}
